EnableExplicit

UsePNGImageDecoder()

Define CustomCursor.I

CompilerSelect #PB_Compiler_OS
  CompilerCase #PB_OS_Linux
    ImportC ""
      gtk_widget_get_window(*Widget.GtkWidget)
    EndImport
  CompilerCase #PB_OS_MacOS
    ImportC ""
      CGEventTapCreate(Tap.I, Place.I, Options.I, EventsOfInterest.Q,
        Callback.I, *UserData)
    EndImport

    Define CursorInWindow.I
    Define EventTap.I
    
    ProcedureC EventTapHandler(Proxy.I, EventType.I, Event.I, *UserData)
      Shared CustomCursor.I
      Shared CursorInWindow.I

      If CustomCursor
        If WindowMouseX(0) = -1 Or WindowMouseY(0) = -1
          If CursorInWindow = #True
            CocoaMessage(0, CocoaMessage(0, 0, "NSCursor arrowCursor"), "set")
            CursorInWindow = #False
          EndIf
        Else
          If CursorInWindow = #False
            CocoaMessage(0, CustomCursor, "set")
            CursorInWindow = #True
          EndIf
        EndIf
      EndIf
    EndProcedure

    EventTap = CGEventTapCreate(0, 0, 1, #NSMouseMovedMask, @EventTapHandler(),
      0)
    
    If EventTap
      CocoaMessage(0, CocoaMessage(0, 0, "NSRunLoop currentRunLoop"),
        "addPort:", EventTap, "forMode:$", @"kCFRunLoopDefaultMode")
    EndIf
CompilerEndSelect

Procedure ChangeCursorToPNGImage(WindowID.I, ImageID.I)
  Shared CustomCursor.I

  CompilerSelect #PB_Compiler_OS
    CompilerCase #PB_OS_Linux
      CustomCursor = gdk_cursor_new_from_pixbuf_(gdk_display_get_default_(),
        ImageID(ImageID), 0, 0)
      gdk_window_set_cursor_(gtk_widget_get_window(WindowID(WindowID)),
        CustomCursor)
    CompilerCase #PB_OS_MacOS
      Protected Hotspot.NSPoint
      
      Hotspot\x = 1
      Hotspot\y = 1
      CustomCursor = CocoaMessage(0, 0, "NSCursor alloc")
      CocoaMessage(0, CustomCursor,
        "initWithImage:", ImageID(ImageID),
        "hotSpot:@", @Hotspot)
      CocoaMessage(0, CustomCursor, "set")
      CocoaMessage(0, WindowID(WindowID), "disableCursorRects")
    CompilerCase #PB_OS_Windows
      Protected Cursor.ICONINFO

      Cursor\fIcon = #False
      Cursor\xHotspot = 1
      Cursor\yHotspot = 1
      Cursor\hbmColor = ImageID(ImageID)
      Cursor\hbmMask = ImageID(ImageID)
      CustomCursor = CreateIconIndirect_(Cursor)
      SetClassLongPtr_(WindowID(WindowID), #GCL_HCURSOR, CustomCursor)
      
      
     ; SetClassLongPtr_(WindowID(WindowID), #GCLP_HCURSOR, CustomCursor)
      
;  Protected CrossCur=LoadCursor_(0,#IDC_ARROW)
;Protected Cross=CopyImage_(CrossCur,#IMAGE_CURSOR,0,0,#LR_COPYFROMRESOURCE)

; Protected MyCross=CopyImage_(Cross,#IMAGE_CURSOR,0,0,#LR_COPYFROMRESOURCE)
; SetSystemCursor_(MyCross, #OCR_NORMAL)

 ;SetSystemCursor_(Cross, #OCR_NORMAL)
     ; SetSystemCursor_(CustomCursor, #OCR_NORMAL)
  CompilerEndSelect
EndProcedure

If LoadImage(0, #PB_Compiler_Home + "examples/sources/Data/world.png") = 0
  MessageRequester("Error",
    "Loading of image World.png failed!",
    #PB_MessageRequester_Error)
  End
EndIf

OpenWindow(0, 200, 100, 290, 120, "Display custom cursor")
ButtonGadget(0, WindowWidth(0) / 2 - 120, 40, 240, 25,
  "Change cursor to PNG image")

Repeat
  Select WaitWindowEvent()
    Case #PB_Event_CloseWindow
      Break
    Case #PB_Event_Gadget
      If EventGadget() = 0 And EventType() = #PB_EventType_LeftClick
        ChangeCursorToPNGImage(0, 0)
        ;DisableGadget(0, #True)
        SetWindowTitle(0, "Cursor changed into PNG image")
      EndIf
  EndSelect
ForEver